// Script that creates a pen
//
// For C/C++ examples and docs go to:
//  https://github.com/SanderMertens/flecs

using flecs.script.*
using flecs.components.*

template FencePost {
  prop x: 0.0
  Position3: {x, 1}
  Box: {0.5, 2, 0.5}
}

template FenceBoard {
  prop width: 10.0
  prop y: 1.0
  Position3: {0, y}
  Box: {width, 0.5, 0.2}
}

template Fence {
  prop width: 10.0
  const post_count = i32: 2 + width / 5
  const post_spacing: width / (post_count - 1)
  
  with Rgb(0.15, 0.1, 0.05) {
    for i in 0..post_count {
      FencePost(i * post_spacing - width / 2)
    }
    
    FenceBoard(width, 0.5)
    FenceBoard(width, 1.5)
  }
}

template Pen {
  prop width: 10.0
  prop depth: 10.0
  
  Fence(width) { Position3: {z: 0.5 * -depth} }
  Fence(width) { Position3: {z: 0.5 * depth} }

  with Rotation3(y: PI / 2) {
    Fence(depth) { Position3: {x: 0.5 * -width} }
    Fence(depth) { Position3: {x: 0.5 * width} }
  }
}

Pen my_pen(width: 10, depth: 15)
